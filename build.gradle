plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'sejong'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.projectlombok:lombok:1.18.26'
    compileOnly 'org.projectlombok:lombok:1.18.26'
    implementation 'org.postgresql:postgresql:42.7.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'io.awspring.cloud:spring-cloud-starter-aws:2.4.4'
}
tasks.named('bootJar') {
    mainClass = 'sejong.globalbuddy.GlobalbuddyApplication'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.release = 17
}

def frontendDir = "$projectDir/src/main/frontend"


//React 빌드 연동

task installReact(type: Exec) {
    workingDir "$frontendDir"
    group = BasePlugin.BUILD_GROUP

    // OS 구분 처리
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine "npm.cmd", "install"
    } else {
        commandLine "npm", "install"
    }

    // node_modules 없으면 실행하도록 설정
    onlyIf { !file("$frontendDir/node_modules").exists() }
}

task buildReact(type: Exec) {
    dependsOn installReact
    workingDir "$frontendDir"
    environment 'NODE_OPTIONS', '--max-old-space-size=4096'
    //노드 힙 사이즈 키워서 빌드
    group = BasePlugin.BUILD_GROUP

    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine "npm.cmd", "run-script", "build"
    } else {
        commandLine "npm", "run-script", "build"
    }
}

task cleanStatic(type: Delete) {
    delete "$projectDir/src/main/resources/static"
}

task copyReactBuildFiles(type: Copy) {
    dependsOn buildReact, cleanStatic
    from "$frontendDir/build"
    into "$projectDir/src/main/resources/static"
}

processResources {
    dependsOn copyReactBuildFiles
}
